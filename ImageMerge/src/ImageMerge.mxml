<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   width="1000" height="720" 
					   applicationComplete="init(event)"
					   >
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import com.YFFramework.core.event.YFEventCenter;
			import com.YFFramework.core.net.loader.image_swf.UILoader;
			import com.YFFramework.core.proxy.StageProxy;
			import com.YFFramework.core.ui.utils.Draw;
			import com.YFFramework.core.utils.UtilString;
			
			import mx.collections.ArrayCollection;
			import mx.events.ColorPickerEvent;
			import mx.events.FlexEvent;
			
			import spark.events.IndexChangeEvent;
			
			private var _dict:Dictionary;
			
			//保存文具盒
			private var _saveFile:File=File.desktopDirectory;
			
			
			[Bindable]
			private static var collection:ArrayCollection=new ArrayCollection([32,64,128,256,512,1024,2048]);
			protected function init(event:FlexEvent):void
			{
				_dict=new Dictionary();
				addEventListener(NativeDragEvent.NATIVE_DRAG_DROP,onDragDrop);
				addEventListener(NativeDragEvent.NATIVE_DRAG_ENTER,onDragDrop);
				
				_saveFile.addEventListener(Event.SELECT,onSelect);
					
				YFEventCenter.Instance.dispatchEventWith("stageInit");
				StageProxy.Instance.configure(stage);

				draw();
			}
			
			private function onDragDrop(e:NativeDragEvent):void
			{
				//将拖入的文件以数组形式获得，指定拖入的数据是文件数组形式
				var files:Array = e.clipboard.getData(ClipboardFormats.FILE_LIST_FORMAT) as Array;
				//获得拖入的第一个文件
				var file:File = File(files[0]);
				switch(e.type)
				{				
					case NativeDragEvent.NATIVE_DRAG_ENTER:  
						if(file.type==".png")
							NativeDragManager.acceptDragDrop(this);
						break;
					case NativeDragEvent.NATIVE_DRAG_DROP:
						initLoad(file)
						break;
				}
			} 
			
			
			private function reset():void
			{
				_dict=new Dictionary();
				myRoot.removeAllElements();
			}
			
			
			private function initLoad(file:File):void
			{
				var name:String=UtilString.getExactName(file.name);
				var loader:UILoader=new UILoader();
				loader.loadCompleteCallback=callBack;
				loader.initData(file.url,null,{name:name,x:stage.mouseX,y:stage.mouseY});
			}
			private function callBack(content:Bitmap,data:Object):void
			{
				var name:String=data.name;
				var imageEx:ImageEx=new ImageEx();
				imageEx.source=content.bitmapData;
				_dict[name]=imageEx;
				imageEx.x=data.x-content.bitmapData.width*0.5;
				imageEx.y=data.y-content.bitmapData.height*0.5;
				myRoot.addElement(imageEx);
			}
			
			
			
			/**画背景
			 */ 
			private function drawBg(event:ColorPickerEvent=null):void
			{	
				draw();
			}
			
			protected function change(event:IndexChangeEvent=null):void
			{
				draw();
			}
			
			private function draw(e:FlexEvent=null):void
			{
				Draw.DrawRect(bg.graphics,wCombo.selectedItem,hCombo.selectedItem,colorPicker.selectedColor);
				bg.width=width;
				bg.height=height;
			}
			
			
			
			protected function onClick(event:MouseEvent):void
			{
				switch(event.currentTarget)
				{
					case newBtn:
						reset();
						break;
					case saveBtn:
						_saveFile.browseForDirectory("保存");
						break;
				}
			}
			
			private function onSelect(e:Event):void
			{
				var dir:File=File.desktopDirectory;
				dir.url=_saveFile.url;
				ImageCreator.create(myRoot,dir,filenameTxt.text,_dict,wCombo.selectedItem,hCombo.selectedItem);
			}
					
			
		]]>
	</fx:Script>
	<s:Scroller x="10" y="30" width="900" height="650">
		<s:Group width="100%" height="100%" id="container" >
			<s:Group width="100%" height="100%" id="bg" />
			<s:Group width="100%" height="100%" id="myRoot" />
		</s:Group>
	</s:Scroller>
	<mx:ColorPicker id="colorPicker" x="25" y="2" change="drawBg(event)" selectedColor="#C6B2EC" />
	<s:Label x="87" y="10" text="W"/>
	<s:Label x="222" y="10" text="H"/>
	<s:ComboBox  x="106" y="4" width="67" id="wCombo" dataProvider="{collection}" selectedItem="512" change="change(event)"/>
	<s:ComboBox  x="239" y="4" width="64" id="hCombo" dataProvider="{collection}" selectedItem="256" change="change(event)" />
	<s:Button id="newBtn" x="334" y="5" label="新建" click="onClick(event)"/>
	<s:Button id="saveBtn" x="414" y="5" label="保存" click="onClick(event)"/>
	<s:TextInput id="filenameTxt" x="510" y="5" width="79" color="#080000" prompt="文件名"/>
</s:WindowedApplication>
